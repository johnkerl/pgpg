!whitespace : ' ' | '\t' | '\n' | '\r' ;
!comment : '#'  {.} '\n' ;

Root
    : NounPhrase TransitiveVerbPhrase NounPhrase
    | NounPhrase IntransitiveVerbPhrase
    | TransitiveImperativeVerbPhrase NounPhrase
    | IntransitiveImperativeVerbPhrase
;

NounPhrase
    : NounPhraseWithoutArticle
    | Article NounPhraseWithoutArticle
;

NounPhraseWithoutArticle
    : Noun
    | Adjective NounPhraseWithoutArticle
;

TransitiveVerbPhrase
    : TransitiveVerb
    | Adverb TransitiveVerbPhrase
;

IntransitiveVerbPhrase
    : IntransitiveVerb
    | Adverb IntransitiveVerbPhrase
;

TransitiveImperativeVerbPhrase
    : TransitiveImperativeVerb
    | Adverb TransitiveImperativeVerbPhrase
;

IntransitiveImperativeVerbPhrase
    : IntransitiveImperativeVerb
    | Adverb IntransitiveImperativeVerbPhrase
;

Noun                       : "dog"     | "cat"    | "mouse"  | "fox"    | "food"  | "book" ;
Adjective                  : "red"     | "green"  | "brown"  | "quick"  | "lazy";
Article                    : "the"     | "a"      ;
TransitiveVerb             : "goes"    | "puts"   | "eats"   ;
IntransitiveVerb           : "walks"   | "runs"   | "sleeps" | "jumps";
TransitiveImperativeVerb   : "put"     | "read"   | "eat"    ;
IntransitiveImperativeVerb : "go"      | "jump";
Adverb                     : "quickly" | "slowly" ;
