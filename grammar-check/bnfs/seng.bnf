!whitespace : ' ' | '\t' | '\n' | '\r' ;
!comment : '#'  {.} '\n' ;

Root
    : NounPhrase TransitiveVerbPhrase NounPhrase
    | NounPhrase IntransitiveVerbPhrase
    | TransitiveImperativeVerbPhrase NounPhrase
    | IntransitiveImperativeVerbPhrase
;

NounPhrase
    : NounPhraseWithoutArticle
    | Article NounPhraseWithoutArticle
;

NounPhraseWithoutArticle
    : Noun
    | Adjective NounPhraseWithoutArticle
;

TransitiveVerbPhrase
    : TransitiveVerb
    | Adverb TransitiveVerbPhrase
    // Shift-reduce conflicts, alas :)
    // | Adverb TransitiveVerbPhrase Preposition NounPhrase
    // | TransitiveVerbPhrase Preposition NounPhrase
;

IntransitiveVerbPhrase
    : IntransitiveVerb
    | Adverb IntransitiveVerbPhrase
    | IntransitiveVerb Preposition NounPhrase
;

TransitiveImperativeVerbPhrase
    : TransitiveImperativeVerb
    | Adverb TransitiveImperativeVerbPhrase
    // Shift-reduce conflicts, alas :)
    // | Adverb TransitiveImperativeVerbPhrase Preposition NounPhrase
    // | TransitiveImperativeVerbPhrase Preposition NounPhrase
;

IntransitiveImperativeVerbPhrase
    : IntransitiveImperativeVerb
    | Adverb IntransitiveImperativeVerbPhrase
    | IntransitiveImperativeVerb Preposition NounPhrase
;

Noun                       : "dog"     | "cat"    | "mouse"  | "fox"    | "food"  | "book" ;
Adjective                  : "red"     | "green"  | "brown"  | "quick"  | "lazy";
Article                    : "the"     | "a"      ;
TransitiveVerb             : "goes"    | "puts"   | "eats"   ;
IntransitiveVerb           : "walks"   | "runs"   | "sleeps" | "jumps";
TransitiveImperativeVerb   : "put"     | "read"   | "eat"    ;
IntransitiveImperativeVerb : "go"      | "jump";
Adverb                     : "quickly" | "slowly" ;
Preposition                : "under"   | "over" ;
